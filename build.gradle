apply plugin: 'java'
apply plugin: 'gradle-one-jar'
apply plugin: 'application'
apply plugin: 'maven'

group = 'io.ei.jsontoxls'
version = '0.2-SNAPSHOT'
ext.packaging = 'jar'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

mainClassName = 'io.ei.jsontoxls.JsonToXlsService'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.3'
    }
}

project.ext {
    dropwizardVersion = '0.6.2'
    mainClassName = 'io.ei.jsontoxls.JsonToXlsService'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    compile group: 'net.sf.jxls', name: 'jxls-core', version: '1.0.2'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.2.4'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
}


jar {

    manifest {
        attributes "Implementation-Title": "JSON-to-XLS"
        attributes "Implementation-Version": version
        attributes "Main-Class" : mainClassName
    }

    // remove the security files (from mail.jar / activation.jar) so that the jar will be executable.
    doFirst {
        from (configurations.runtime.resolve().collect { it.isDirectory() ? it : zipTree(it) }) {
            exclude 'META-INF/MANIFEST.MF'
            exclude 'META-INF/*.SF'
            exclude 'META-INF/*.DSA'
            exclude 'META-INF/*.RSA'
        }
    }
}

run {
    args 'server', 'json-to-xls.yml'
}


uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://nexus.motechproject.org/content/repositories/json-to-xls") {
                authentication(userName: System.getenv('NEXUS_USERNAME'), password: System.getenv('NEXUS_PASSWORD'))
            }
        }

    }
}